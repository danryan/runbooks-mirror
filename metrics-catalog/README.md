# Metrics Catalog

This folder contains definitions used to autogenerate key metrics for each
service in the GitLab service catalog

## Structure

Each service in the application should have a matching file in the `services`
directory. The file should match the service identifier (its `type` label).

Each service has a set of "components". For example, for the `web` service,
there are components for `puma` and `workhorse`.

The service metrics are generated by aggregating the component metrics in each
service.

The aggregation is done as follows:

1. Service apdex is calculated using a weighted average of the component apdex scores. The
   weight is calculated from the operation rate.
1. Service error ratio is calculated as the sum of all components over the sum of all component operations.
1. Component operation rate is the sum of all component operations. Note that this may be confusing as a single operation may be double or triple counted.
   Consider this value to be fairly arbitrary. We do not generate pagerduty alerts based on this calculation.
1. Saturation and utilization metrics cannot be aggregated and are individually represented at the service level.

## Defining Service Monitoring

This example shows how service monitoring is defined for a service.

For more examples, review `services/*.jsonnet`.

```jsonnet
metricsCatalog.serviceDefinition({
  type: 'service_type',     // Tehe `type` identifier label for the service
  tier: 'sv',               // The `tier` identifier label for the service
  contractualThresholds: {  // Thresholds are used to calculate SLA metrics (selected services only)
    apdexRatio: 0.95,       // Apdex must be above this value for the service to be in SLA
    errorRatio: 0.005,      // Error ratio must be below this value for the service to in SLA
  },
  monitoringThresholds: {   // Monitoring thresholds are used for multiwindow, multi-burnrate alerting
    apdexScore: 0.999,      // Ratio of requests (in a one month period) that should meet their apdex thresholds
    errorRatio: 0.9999,     // Ratio of requests (in a one month period) that should complete without error
  },
  serviceDependencies: {    // A map of (downstream) services on which this service depends
    gitaly: true,
    praefect: true,
  },
  components: {             // A map of components for this service. The name is defined by the key
    workhorse: {            // Defines a service called `workhorse`
      // Each component must have a `requestRate` metric and optionally can have
      // an `apdex` (for latency) and an `errorRate` metric too.

      // Include an optional apdex score for this component...
      // `histogramApdex` declares the apdex metric is calculated from a histogram
     apdex: histogramApdex(                                                // See https://prometheus.io/docs/practices/histograms/#apdex-score
        histogram='gitlab_workhorse_http_request_duration_seconds_bucket', // The _bucket histogram metric to use
        selector='job="gitlab-workhorse-web"',                             // Any selectors to filter the metric with
        satisfiedThreshold=1,                                              // The bucket (eg `le="1"`) to use for the satisfactory threshold
        toleratedThreshold=10
      ),

      // requestRate is mandatory for all components
      // `rateMetric` declares the requestRate is calculated off a rate+counter
      requestRate: rateMetric(
        counter='gitlab_workhorse_http_requests_total',
        selector='job="gitlab-workhorse-web", type="web"'
      ),

      // errorRate is optional
      // `rateMetric` declares the errorRate is calculated off a rate+counter
      errorRate: rateMetric(
        counter='gitlab_workhorse_http_requests_total',
        selector='job="gitlab-workhorse-web", type="web", code=~"^5.*"'
      ),

      // significantLabels are a list of labels that will be used as dimensions in generated dashboards
      // they will also be included in intermediate recording rules
      significantLabels: ['fqdn', 'route'],
    },

    puma: {                 // Defines a service called `workhorse`
      ...
    },
  },
})
```

The metrics catalog will then use this definition to generate key service metrics for each component
and for the service in aggregate.

This includes:

1. Multiple burn rate (currently 5m, 1h, 30m, 6h and -deprecated- 1m) are generated for each metric for each component
1. Aggregated service level metrics
1. Error ratio metrics (calculated from error rate over operation rate)
1. SLA monitoring
1. Multiwindow, multiburn rate alerts. See https://landing.google.com/sre/workbook/chapters/alerting-on-slos/
1. Standardized service overview Grafana dashboards. See https://dashboards.gitlab.com/d/web-main for an example
1. GitLab Dashboards. See https://gitlab.com/gitlab-com/runbooks/-/metrics/key-metrics-web.yml for an example

## Development

Follow the instructions on
[the dashboards' README.md](../dashboards/README.md#local-development) to
install `jsonnet`.

To update the autogenerated file after you make your changes run
`make generate` from the project's root.

If you're adding a new service catalog make sure to append it to the list on the
`services/all.jsonet` file.
