local recordingRules = import 'recording-rules/recording-rules.libsonnet';
local monitoredServices = (import 'gitlab-metrics-config.libsonnet').monitoredServices;

// The service SLO rules map SLOs to static recording rules,
// for use in alerting, dashboards, etc
local serviceSLOs = recordingRules.serviceSLORuleSetGenerator();

// Component mappings are static recording rules which help
// determine whether a component is being monitored. This helps
// prevent spurious alerts when a component is decommissioned.
local serviceMapping = recordingRules.serviceMappingRuleSetGenerator();
local componentMappingRuleSetGenerator = recordingRules.componentMappingRuleSetGenerator();

// Select all services with `autogenerateRecordingRules` (default on)
local selectServices(services) = std.filter(function(service) service.autogenerateRecordingRules, services);
// The SLOs, and component mappings are the same across environments. So they
// are not separated per file

local rules(componentMappingServices, allServices=monitoredServices) =
  std.flatMap(
    function(serviceDefinition)
      serviceSLOs.generateRecordingRulesForService(serviceDefinition),
    selectServices(allServices)
  )
  +
  std.flatMap(
    function(serviceDefinition)
      serviceMapping.generateRecordingRulesForService(serviceDefinition),
    selectServices(allServices)
  )
  +
  std.flatMap(
    function(serviceDefinition)
      componentMappingRuleSetGenerator.generateRecordingRulesForService(serviceDefinition),
    // We can safely exclude dangerouslyThanosEvaluated services
    // from the component mappings, since these are included in the
    // autogenerated-key-metrics-<service>.yml file
    selectServices(componentMappingServices),
  );


{
  rules: rules,
}
