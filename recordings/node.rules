# The count of CPUs per node, useful for getting CPU time as a percent of total.
instance:node_cpus:count = count(node_cpu{mode="idle"}) without (cpu,mode)

# CPU in use by CPU.
instance_cpu:node_cpu_not_idle:rate5m = sum(rate(node_cpu{mode!="idle"}[5m])) without (mode)

# CPU in use by mode.
instance_mode:node_cpu:rate5m = sum(rate(node_cpu[5m])) without (cpu)
instance_mode:node_cpu:rate1h = sum(rate(node_cpu[1h])) without (cpu)

# CPU in use total
instance:node_cpu_in_use:rate5m = sum(rate(node_cpu{mode!="idle"}[5m])) without (cpu, mode)
instance:node_cpu_in_use:rate1h = sum(rate(node_cpu{mode!="idle"}[1h])) without (cpu, mode)

# CPU % in use by node
instance:node_cpu_in_use:percent5m = instance:node_cpu_in_use:rate5m / instance:node_cpus:count
instance:node_cpu_in_use:percent1h = instance:node_cpu_in_use:rate1h / instance:node_cpus:count
instance:node_cpu_in_use:percent_stddev_over_time1h = stddev_over_time(instance:node_cpu_in_use:rate5m[1h])

# Filesystem available ratio for "real" filesystems.
instance:node_filesystem_avail:ratio = node_filesystem_avail{device=~"/dev/.+"} / node_filesystem_size{device=~"/dev/.+"}

# Disk IO rates
instance:node_disk_writes_completed:irate1m = sum(irate(node_disk_writes_completed{device=~"sd.*"}[1m])) without (device)
instance:node_disk_reads_completed:irate1m = sum(irate(node_disk_reads_completed{device=~"sd.*"}[1m])) without (device)
