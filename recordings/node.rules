# The count of CPUs per node, useful for getting CPU time as a percent of total.
instance:node_cpus:count = count(node_cpu{mode="idle"}) without (cpu,mode)

# CPU in use by CPU.
instance_cpu:node_cpu_not_idle:rate5m = sum(rate(node_cpu{mode!="idle"}[5m])) without (mode)

# CPU in use by mode.
instance_mode:node_cpu:rate5m = sum(rate(node_cpu[5m])) without (cpu)

# CPU % in use by node
instance:node_cpu_in_use:ratio = sum(instance_mode:node_cpu:rate5m{mode!="idle"}) without (mode) / instance:node_cpus:count

# Filesystem available ratio for "real" filesystems.
instance:node_filesystem_avail:ratio = node_filesystem_avail{device=~"/dev/.+"} / node_filesystem_size{device=~"/dev/.+"}

# Disk IO rates
instance:node_disk_writes_completed:irate1m = sum(irate(node_disk_writes_completed{device=~"sd.*"}[1m])) without (device)
instance:node_disk_reads_completed:irate1m = sum(irate(node_disk_reads_completed{device=~"sd.*"}[1m])) without (device)

# Number of instances up per fleet
instance:up:count = count(up{job="node", type!=""} == 1) without (instance, fqdn)

